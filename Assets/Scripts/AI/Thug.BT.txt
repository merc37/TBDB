//Comments are above particular node they are referring to the function of
//Because comments on the same line disable that whole line - go figure

tree("Root")
	parallel
		repeat mute tree("Live")
		repeat mute tree("DIE")

tree("Live")
	parallel
		repeat
			mute
				tree("FireOnSight")
		repeat
			mute
				fallback
					//Combat Movement
					sequence
						//Limits Combat to When player can be seen
						tree("PlayerInSight")
						//Combat Movement should only fail if player in sight
						mute
							fallback
								//Should be able to roll when not in combat?
								sequence
									not IsRolling
									ShouldRoll
									not IsRollOnCooldown
									Roll
								//Prevents crashing into player
								sequence
									not PlayerRunningAway
									PlayerInMedAttackRange
									StopMovement
								//Follows player
								sequence
									SetMovementTargetToPlayerPosition
									not ReachedMovementTarget
									MoveTowardsMovementTarget
									//PathToMovementTarget
					//Non Combat Movement
					fallback
						//Go to last known position
						sequence
							SetMovementTargetToPlayerLastKnownPosition
							SetLookTargetToMovementDirection
							fallback
								not ReachedMovementTarget
								sequence
									StopMovement
									mute	SetLookTargetToPlayerLastKnownHeading
									//Prepare to search
									SetSearchDirectionToLookDirection
									SetSearchCenterFromPosition
									SetNextSearchPoint
									ResetSearchTimer
									//UnSet so they stop 
									UnSetPlayerLastKnownHeading
									UnSetPlayerLastKnownPosition
							PathToMovementTarget
						//Search
						sequence
							SetMovementTargetToCurrentSearchPoint
							CheckSearchTimer
							SetLookTargetToMovementDirection
							fallback
								not ReachedMovementTarget
								sequence
									StopMovement
									SetNextSearchPoint
							PathToMovementTarget
						//Idle
						sequence
							StopMovement
							
tree("FireOnSight")
	sequence
		Fail
		tree("PlayerInSight")
		PlayerInAttackRange
		not LowOnAmmo
		AimAtPlayer
		Shoot

tree("DIE")
	sequence
		HasZeroHealth
		DestroySelf

tree("PlayerInSight")
	sequence
		PlayerInSight
		SetPlayerLastKnownPosition
		SetPlayerLastKnownHeading
		SetLookTargetToPlayer

//Potential Future Bugs
//1. If lastKnownPos is still set and tries to move to it w/o being updated
//2. If pathTo returns false path could not be found (should not happen, but not accounted for if it does)
        
        
				