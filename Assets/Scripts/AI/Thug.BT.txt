//Comments are above particular node they are referring to the function of
//Because comments on the same line disable that whole line - go figure

tree("Root")
	parallel
		repeat mute tree("Live")
		repeat mute tree("DIE")

tree("Live")
	parallel
		repeat
			mute
				//Combat Actions
				tree("FireOnSight")
		repeat
			mute
				fallback
					sequence
						not SetMovementTargetToNull
					//Combat
					//Should be able to roll when not in combat?
					sequence
						not IsRolling
						ShouldRoll
						not IsRollOnCooldown
						Roll
						repeat
							IsRolling
					//Strafe
					repeat
						sequence
							tree("PlayerInSight")
							fallback
								sequence
									IsMovementTargetNull
									PlayerInMediumAttackRange
								StrafeCenterInMediumAttackRange
							MoveTowardsMovementTarget
							SetLookTargetToPlayer
							mute
								sequence
									fallback
										CheckMovementStopped
										ReachedMovementTarget
									sequence
										mute
											sequence
												IsMovementTargetNull
												SetStrafeCenter
										SetMovementTargetToStrafePoint
					//Run after
					sequence
						tree("PlayerInSight")
						PlayerInAttackRange
						not LowOnAmmo
						AimAtPlayer
						Shoot
						SetMovementTargetToPlayerPosition
						not ReachedMovementTarget
						MoveTowardsMovementTarget
					//Follows player
					sequence
						tree("PlayerInSight")
						SetMovementTargetToPlayerPosition
						SetLookTargetToPlayer
						not ReachedMovementTarget
						MoveTowardsMovementTarget
						//PathToMovementTarget
					//***Non Combat***
					//Go to last known position
					sequence
						sequence
							IsPlayerLastPositionKnown
							SetMovementTargetToPlayerLastKnownPosition
						SetLookTargetToMovementDirection
						PathToMovementTarget
						mute
							sequence
								ReachedMovementTarget
								sequence
									StopMovement
									mute
										SetLookTargetToPlayerLastKnownHeading
									UnSetPlayerLastKnownHeading
									UnSetPlayerLastKnownPosition
									SetMovementTargetToNull
									//Search
									repeat
										sequence
											not tree("PlayerInSight")
											not IsPlayerLastPositionKnown
											fallback
												sequence
													IsMovementTargetNull
													ResetSearchTimer
												CheckSearchTimer
											mute
												fallback
													sequence
														IsMovementTargetNull
														SetSearchDirectionToLookDirection
														SetSearchCenterFromPosition
														SetMovementTargetToSearchPoint
													sequence
														PathToMovementTarget
														SetLookTargetToMovementDirection
														ReachedMovementTarget
														SetMovementTargetToSearchPoint
					//Idle
					sequence
						StopMovement
							
tree("FireOnSight")
	sequence
		Fail
		tree("PlayerInSight")
		PlayerInAttackRange
		not LowOnAmmo
		AimAtPlayer
		Shoot

tree("DIE")
	sequence
		HasZeroHealth
		DestroySelf

tree("PlayerInSight")
	sequence
		PlayerInSight
		SetPlayerLastKnownPosition
		SetPlayerLastKnownHeading

	
		

//Potential Future Bugs
//1. If lastKnownPos is still set and tries to move to it w/o being updated
//2. If pathTo returns false path could not be found (should not happen, but not accounted for if it does)
        
        
				